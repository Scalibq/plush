
// Benchmark for rendering a list of triangles from vertex and index arrays

let FRAME_WIDTH = 800;
let FRAME_HEIGHT = 600;

// --- 3D Math and Classes ---

let PI = 3.1415926535;

// Converts an angle from degrees to radians
fun deg2rad(deg) {
    return deg * PI / 180.0;
}

class Image {
    init(self, width, height) {
        self.width = width;
        self.height = height;
        self.bytes = ByteArray.with_size(4 * width * height);
    }

    fill(self, color) {
        self.bytes.fill_u32(0, self.width * self.height, color);
    }
}

class Vec3 {
    init(self, x, y, z) {
        self.x = x;
        self.y = y;
        self.z = z;
    }

    add(self, other) {
        return Vec3(self.x + other.x, self.y + other.y, self.z + other.z);
    }

    sub(self, other) {
        return Vec3(self.x - other.x, self.y - other.y, self.z - other.z);
    }

    mul(self, scalar) {
        return Vec3(self.x * scalar, self.y * scalar, self.z * scalar);
    }

    dot(self, other) {
        return self.x * other.x + self.y * other.y + self.z * other.z;
    }

    cross(self, other) {
        return Vec3(
            self.y * other.z - self.z * other.y,
            self.z * other.x - self.x * other.z,
            self.x * other.y - self.y * other.x
        );
    }

    length_squared(self) {
        return self.dot(self);
    }

    normalize(self) {
        let len = self.length_squared().sqrt();
        if (len == 0.0) {
            return Vec3(0.0, 0.0, 0.0);
        }
        return Vec3(self.x / len, self.y / len, self.z / len);
    }
}

// Initializes a 4x4 matrix with zeros
// Matrices are row-major
fun mat4_init(m) {
    for (let var i = 0; i < 4; ++i) {
        let row = Array.with_size(4, 0.0);
        m.push(row);
    }
}

// Transform a 3D point using a 4x4 transformation matrix
fun mat4_mul_vec(i, m) {
    let o = Vec3(0, 0, 0);
    o.x   = i.x * m[0][0] + i.y * m[0][1] + i.z * m[0][2] + m[0][3];
    o.y   = i.x * m[1][0] + i.y * m[1][1] + i.z * m[1][2] + m[1][3];
    o.z   = i.x * m[2][0] + i.y * m[2][1] + i.z * m[2][2] + m[2][3];
    let w = i.x * m[3][0] + i.y * m[3][1] + i.z * m[3][2] + m[3][3];

    if (w != 0.0) {
        o.x = o.x / w;
        o.y = o.y / w;
        o.z = o.z / w;
    }

    return o;
}

// Multiplies two 4x4 matrices
fun mat4_mul(a, b) {
    let result = [];
    mat4_init(result);
    for (let var i = 0; i < 4; ++i) {
        for (let var j = 0; j < 4; ++j) {
            for (let var k = 0; k < 4; ++k) {
                result[i][j] = result[i][j] + a[k][j] * b[i][k];
            }
        }
    }
    return result;
}

// Matrix for a rotation around the X axis
fun mat4_rotx(theta)
{
    let result = [];
    mat4_init(result);

    let cost = theta.cos();
    let sint = theta.sin();

    // First row
    result[0][0] = 1.0;

    // Second
    result[1][1] = cost;
    result[1][2] = -sint;

    // Third row
    result[2][1] = sint;
    result[2][2] = cost;

    return result;
}

// Matrix for a rotation around the Y axis
fun mat4_roty(theta)
{
    let result = [];
    mat4_init(result);

    let cost = theta.cos();
    let sint = theta.sin();

    // First row
    result[0][0] = cost;
    result[0][2] = sint;

    // Second row
    result[1][1] = 1.0;

    // Third row
    result[2][0] = -sint;
    result[2][2] = cost;

    return result;
}

// Generate a perspective projection matrix identical to that
// produced by the gluPerspective() function.
// This is used to project coordinates from eye space into clip space.
// We follow OpenGL conventions.
//
// aspect = screen_width / screen_height
// fovy is in radians
//
// +Y points up,
// +X points to the right,
// -Z points into the screen.
//
// Points are projected such that X/Y are in [-1, 1]
// Z values between [near, far] get projected into [-1, 1]
// Points behind the near plane should be discarded (clipped) ahead of time.
//
fun perspective(fovy_rad, aspect, near, far) {
    let m = [];
    mat4_init(m);

    let f = 1.0 / (fovy_rad / 2.0).tan();
    let nf = 1.0 / (near - far);

    m[0][0] = f / aspect;
    m[0][1] = 0.0;
    m[0][2] = 0.0;
    m[0][3] = 0.0;

    m[1][0] = 0.0;
    m[1][1] = f;
    m[1][2] = 0.0;
    m[1][3] = 0.0;

    m[2][0] = 0.0;
    m[2][1] = 0.0;
    m[2][2] = (far + near) * nf;
    m[2][3] = 2.0 * far * near * nf;

    m[3][0] = 0.0;
    m[3][1] = 0.0;
    m[3][2] = -1.0;
    m[3][3] = 0.0;

    return m;
}

// Creates a view matrix that looks at a target from a given position
fun lookat(eye, target, up) {
    let forward = target.sub(eye).normalize();
    let right = forward.cross(up).normalize();
    let new_up = right.cross(forward);

    let m = [];
    mat4_init(m);

    m[0][0] = right.x;
    m[0][1] = right.y;
    m[0][2] = right.z;
    m[0][3] = -right.dot(eye);

    m[1][0] = new_up.x;
    m[1][1] = new_up.y;
    m[1][2] = new_up.z;
    m[1][3] = -new_up.dot(eye);

    m[2][0] = -forward.x;
    m[2][1] = -forward.y;
    m[2][2] = -forward.z;
    m[2][3] = forward.dot(eye);

    m[3][0] = 0.0;
    m[3][1] = 0.0;
    m[3][2] = 0.0;
    m[3][3] = 1.0;

    return m;
}

// Represents a 3D camera
class Camera {
    // Initializes the camera
    init(self, pos, yaw, pitch, fov_x, near, far) {
        self.pos = pos;
        self.yaw = yaw;
        self.pitch = pitch;
        self.fov_x = fov_x;
        self.near = near;
        self.far = far;
    }

    // Calculates the view matrix for the camera
    view_matrix(self) {
        // Yaw is the left/right angle
        let yaw_rad = deg2rad(self.yaw);

        // Pitch is the up/down angle
        let pitch_rad = deg2rad(self.pitch);

        // Rotate the camera direction vector
        let dir = Vec3(0.0, 0.0, -1.0);
        let roty = mat4_roty(yaw_rad);
        let rotx = mat4_rotx(pitch_rad);
        let mat_rot = mat4_mul(roty, rotx);
        let dir_rot = mat4_mul_vec(dir, mat_rot);

        let target = self.pos.add(dir_rot);
        let up = Vec3(0.0, 1.0, 0.0);

        return lookat(self.pos, target, up);
    }

    // Calculates the projection matrix for the camera
    projection_matrix(self, aspect) {
        let fov_x_rad = deg2rad(self.fov_x);
        let fov_y_rad = 2 * ((fov_x_rad / 2.0).tan() / aspect).atan();
        return perspective(fov_y_rad, aspect, self.near, self.far);
    }
}

// --- Triangle Rasterization ---

// This function expects the coordinates to be floats such that
// x is in [0, FRAME_WIDTH] and +x points right
// y is in [0, FRAME_HEIGHT] and +y points down
fun rasterize_triangle(v0, v1, v2, color, image) {
    let var p0 = v0;
    let var p1 = v1;
    let var p2 = v2;
    if (p0.y > p1.y) { let tmp = p0; p0 = p1; p1 = tmp; }
    if (p0.y > p2.y) { let tmp = p0; p0 = p2; p2 = tmp; }
    if (p1.y > p2.y) { let tmp = p1; p1 = p2; p2 = tmp; }

    let y0 = p0.y.floor();
    let y1 = p1.y.floor();
    let y2 = p2.y.floor();

    if (y0 == y2) return;

    let x0 = p0.x;
    let x1 = p1.x;
    let x2 = p2.x;

    let var inv_slope1 = 0.0;
    if (p1.y > p0.y) inv_slope1 = (x1 - x0) / (p1.y - p0.y);

    let var inv_slope2 = 0.0;
    if (p2.y > p0.y) inv_slope2 = (x2 - x0) / (p2.y - p0.y);

    let start_scanline_y_top = y0.max(0);
    let end_scanline_y_top = y1.min(FRAME_HEIGHT);

    if (start_scanline_y_top < end_scanline_y_top) {
        let var cur_x1 = x0 + (start_scanline_y_top - y0) * inv_slope1;
        let var cur_x2 = x0 + (start_scanline_y_top - y0) * inv_slope2;

        for (let var scanline_y = start_scanline_y_top; scanline_y < end_scanline_y_top; ++scanline_y) {
            let start_x = cur_x1.min(cur_x2).floor().max(0);
            let end_x = cur_x1.max(cur_x2).floor().min(FRAME_WIDTH);
            if (start_x < end_x) {
                image.bytes.fill_u32(scanline_y * FRAME_WIDTH + start_x, end_x - start_x, color);
            }
            cur_x1 = cur_x1 + inv_slope1;
            cur_x2 = cur_x2 + inv_slope2;
        }
    }

    let var inv_slope3 = 0.0;
    if (p2.y > p1.y) inv_slope3 = (x2 - x1) / (p2.y - p1.y);

    let start_scanline_y_bot = y1.max(0);
    let end_scanline_y_bot = y2.min(FRAME_HEIGHT);

    if (start_scanline_y_bot < end_scanline_y_bot) {
        let var cur_x1 = x1 + (start_scanline_y_bot - y1) * inv_slope3;
        let var cur_x2 = x0 + (start_scanline_y_bot - y0) * inv_slope2;

        for (let var scanline_y = start_scanline_y_bot; scanline_y < end_scanline_y_bot; ++scanline_y) {
            let start_x = cur_x1.min(cur_x2).floor().max(0);
            let end_x = cur_x1.max(cur_x2).floor().min(FRAME_WIDTH);
            if (start_x < end_x) {
                image.bytes.fill_u32(scanline_y * FRAME_WIDTH + start_x, end_x - start_x, color);
            }
            cur_x1 = cur_x1 + inv_slope3;
            cur_x2 = cur_x2 + inv_slope2;
        }
    }
}

// --- Procedural Data Generation ---

let var lcg_seed = 1;
fun rand_init(seed) { lcg_seed = seed & 0x7FFFFFFF; }
fun rand_float(min_val, max_val) {
    lcg_seed = (lcg_seed * 1103515245 + 12345) & 0x7FFFFFFF;
    let val = lcg_seed.to_f() / 0x7FFFFFFF.to_f();
    return min_val + val * (max_val - min_val);
}

class RenderData {
    init(self) {
        self.vertices = [];
        self.indices = [];
        self.colors = [];
    }

    transform(self, mat) {
        for (let var i = 0; i < self.vertices.len; ++i) {
            let v = mat4_mul_vec(self.vertices[i], mat);

            // We need to invert Y to match screen coordinates
            v.x = (v.x + 1) * 0.5 * FRAME_WIDTH;
            v.y = (1 - v.y) * 0.5 * FRAME_HEIGHT;

            self.vertices[i] = v;
        }
    }

    // Sort the triangles by depth so that they can be rendered back to front
    sort_triangles(self) {
        let num_triangles = self.indices.len _/ 3;
        if (num_triangles == 0) {
            return;
        }

        // Calculate the depth of each triangle
        let depths = Array.with_size(num_triangles, 0.0);
        for (let var i = 0; i < num_triangles; ++i) {
            let v0 = self.vertices[self.indices[i*3]];
            let v1 = self.vertices[self.indices[i*3+1]];
            let v2 = self.vertices[self.indices[i*3+2]];
            depths[i] = (v0.z + v1.z + v2.z) / 3.0;
        }

        // Partition function for quicksort (adapted for triangles)
        fun partition(low, high) {
            let pivot = depths[high];
            let var i = low - 1;

            for (let var j = low; j < high; ++j) {
                // Sort back to front (larger Z is further away)
                if (depths[j] > pivot) {
                    i = i + 1;

                    // Swap depths
                    let temp_depth = depths[i];
                    depths[i] = depths[j];
                    depths[j] = temp_depth;

                    // Swap colors
                    let temp_color = self.colors[i];
                    self.colors[i] = self.colors[j];
                    self.colors[j] = temp_color;

                    // Swap indices (3 per triangle)
                    let i_idx = i * 3;
                    let j_idx = j * 3;
                    let temp_idx0 = self.indices[i_idx];
                    let temp_idx1 = self.indices[i_idx+1];
                    let temp_idx2 = self.indices[i_idx+2];
                    self.indices[i_idx]   = self.indices[j_idx];
                    self.indices[i_idx+1] = self.indices[j_idx+1];
                    self.indices[i_idx+2] = self.indices[j_idx+2];
                    self.indices[j_idx]   = temp_idx0;
                    self.indices[j_idx+1] = temp_idx1;
                    self.indices[j_idx+2] = temp_idx2;
                }
            }

            // Swap the pivot element
            let i1 = i + 1;
            let temp_depth = depths[i1];
            depths[i1] = depths[high];
            depths[high] = temp_depth;

            let temp_color = self.colors[i1];
            self.colors[i1] = self.colors[high];
            self.colors[high] = temp_color;

            let i1_idx = i1 * 3;
            let high_idx = high * 3;
            let temp_idx0 = self.indices[i1_idx];
            let temp_idx1 = self.indices[i1_idx+1];
            let temp_idx2 = self.indices[i1_idx+2];
            self.indices[i1_idx]   = self.indices[high_idx];
            self.indices[i1_idx+1] = self.indices[high_idx+1];
            self.indices[i1_idx+2] = self.indices[high_idx+2];
            self.indices[high_idx]   = temp_idx0;
            self.indices[high_idx+1] = temp_idx1;
            self.indices[high_idx+2] = temp_idx2;

            return i1;
        }

        // Quicksort implementation
        fun quicksort(low, high) {
            if (low < high) {
                let pi = partition(low, high);
                quicksort(low, pi - 1);
                quicksort(pi + 1, high);
            }
        }

        quicksort(0, num_triangles - 1);
    }
}

fun generate_cube(data, center_x, y, center_z, size_x, size_y, size_z, color) {
    let base_idx = data.vertices.len;

    let half_x = size_x / 2.0;
    let half_z = size_z / 2.0;

    // Vertices of the cube
    data.vertices.push(Vec3(center_x - half_x, y,        center_z - half_z)); // 0
    data.vertices.push(Vec3(center_x + half_x, y,        center_z - half_z)); // 1
    data.vertices.push(Vec3(center_x + half_x, y,        center_z + half_z)); // 2
    data.vertices.push(Vec3(center_x - half_x, y,        center_z + half_z)); // 3
    data.vertices.push(Vec3(center_x - half_x, y + size_y, center_z - half_z)); // 4
    data.vertices.push(Vec3(center_x + half_x, y + size_y, center_z - half_z)); // 5
    data.vertices.push(Vec3(center_x + half_x, y + size_y, center_z + half_z)); // 6
    data.vertices.push(Vec3(center_x - half_x, y + size_y, center_z + half_z)); // 7

    // Indices for the 12 triangles of the cube
    // Bottom face
    //data.indices.push(base_idx + 0); data.indices.push(base_idx + 1); data.indices.push(base_idx + 2);
    //data.indices.push(base_idx + 0); data.indices.push(base_idx + 2); data.indices.push(base_idx + 3);
    // Top face
    data.indices.push(base_idx + 7); data.indices.push(base_idx + 6); data.indices.push(base_idx + 5);
    data.indices.push(base_idx + 7); data.indices.push(base_idx + 5); data.indices.push(base_idx + 4);
    // Front face
    data.indices.push(base_idx + 3); data.indices.push(base_idx + 2); data.indices.push(base_idx + 6);
    data.indices.push(base_idx + 3); data.indices.push(base_idx + 6); data.indices.push(base_idx + 7);
    // Back face
    data.indices.push(base_idx + 4); data.indices.push(base_idx + 5); data.indices.push(base_idx + 1);
    data.indices.push(base_idx + 4); data.indices.push(base_idx + 1); data.indices.push(base_idx + 0);
    // Left face
    data.indices.push(base_idx + 0); data.indices.push(base_idx + 3); data.indices.push(base_idx + 7);
    data.indices.push(base_idx + 0); data.indices.push(base_idx + 7); data.indices.push(base_idx + 4);
    // Right face
    data.indices.push(base_idx + 6); data.indices.push(base_idx + 2); data.indices.push(base_idx + 1);
    data.indices.push(base_idx + 6); data.indices.push(base_idx + 1); data.indices.push(base_idx + 5);

    // Add colors for each triangle
    for (let var i = 0; i < 12; ++i) {
        data.colors.push(color);
    }
}

fun generate_city(data, grid_size)
{
    let cell_size = 100.0;
    let building_padding = 22.0;
    let max_height = 350.0;
    let building_base_size = cell_size - building_padding * 2.0;

    for (let var iz = 0; iz < grid_size; ++iz) {
        for (let var ix = 0; ix < grid_size; ++ix) {
            let center_x = ix * cell_size + cell_size / 2.0;
            let center_z = iz * cell_size + cell_size / 2.0;

            let height = rand_float(50.0, max_height);
            let gray = rand_float(0.3, 0.8);
            let color_val = (gray * 255).floor();
            let color = 0xFF000000 | (color_val << 16) | (color_val << 8) | color_val;

            generate_cube(
                data,
                center_x,
                0.0,
                center_z,
                building_base_size,
                height,
                building_base_size,
                color
            );
        }
    }
}




// --- Main ---

let window = $window_create(FRAME_WIDTH, FRAME_HEIGHT, "Procedural City Example", 0);
let image = Image(FRAME_WIDTH, FRAME_HEIGHT);

// Generate city
let data = RenderData();
generate_city(data, 10);

// Camera setup
// The camera looks towards negative Z
let aspect = FRAME_WIDTH.to_f() / FRAME_HEIGHT.to_f();
let camera = Camera(
    Vec3(500.0, 400.0, 1600.0),
    0.0,    // yaw
   -3.0,    // pitch
    90.0,   // fov
    0.1,
    10_000.0
);
let mat_view = camera.view_matrix();
let mat_proj = camera.projection_matrix(aspect);
let mat_vp = mat4_mul(mat_view, mat_proj);













// Clear background
image.fill(0xFF101020);

// Transform all vertices once
let start_transform = $time_current_ms();
data.transform(mat_vp);
data.sort_triangles();
let end_transform = $time_current_ms();

// Render all triangles
let start_render = $time_current_ms();
for (let var i = 0; i < data.indices.len; i = i + 3) {
    let v0 = data.vertices[data.indices[i]];
    let v1 = data.vertices[data.indices[i+1]];
    let v2 = data.vertices[data.indices[i+2]];
    rasterize_triangle(v0, v1, v2, data.colors[i _/ 3], image);
}
let end_render = $time_current_ms();

let transform_ms = end_transform - start_transform;
let render_ms = end_render - start_render;

$println("Transforming " + data.vertices.len.to_s() + " vertices took " + transform_ms.to_s() + " ms.");
$println("Rendering " + (data.indices.len / 3).to_s() + " triangles took " + render_ms.to_s() + " ms.");

$window_draw_frame(window, image.bytes);

// Event loop to keep the window open
loop {
    let msg = $actor_recv();
    if (msg instanceof UIEvent && (msg.kind == 'CLOSE_WINDOW' || (msg.kind == 'KEY_DOWN' && msg.key == 'ESCAPE'))) {
        break;
    }
}
